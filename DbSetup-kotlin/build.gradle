buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.0.2"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.8"
    }
}

apply plugin: 'kotlin'
apply plugin: 'org.jetbrains.dokka'

description = 'Kotlin extensions for DbSetup'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:1.0.2"
    compile project(':DbSetup')
    testCompile 'junit:junit:4.11'
}

dokka {
    inputs.dir 'src/main/kotlin'
    jdkVersion = 8
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
    inputs.dir 'src/main/kotlin'
    jdkVersion = 8
}

task dokkaJar(type: Jar, dependsOn: dokka) {
    classifier = 'dokka'
    from dokka.outputDirectory
    manifest {
        from project.ext.sharedManifest
    }
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
    manifest {
        from project.ext.sharedManifest
    }
}

artifacts {
    archives javadocJar
    archives dokkaJar
}

checkstyleMain {
    source sourceSets.main.kotlin
}

def configurePomDependencies(pom) {
    // we don't care about test dependencies
    pom.dependencies = pom.dependencies.findAll {it.scope != 'test'}
    // all dependencies are compile time dependencies, and there is no need to say it
    pom.dependencies*.scope = null
}

